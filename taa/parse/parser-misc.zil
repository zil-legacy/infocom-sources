<INCLUDE "BASEDEFS" "PARSERDEFS">

<USE "PARSER" "NEWSTRUC" "PMEM">

<FILE-FLAGS MDL-ZIL?>

<DEFMAC RFATAL ()
	'<PROG () <PUSH 2> <RSTACK>>>

<DEFMAC WT? ('PTR 'BIT "OPT" 'B1)
  <COND (<AND <TYPE? .BIT GVAL>
	      <=? <MEMBER "PS?" <SPNAME <CHTYPE .BIT ATOM>>>
		  <SPNAME <CHTYPE .BIT ATOM>>>>
	 <SET BIT <REST <SPNAME <CHTYPE .BIT ATOM>> 3>>)>
  <SET BIT <GET-CLASSIFICATION .BIT>>
  <COND (<OR <NOT <ASSIGNED? B1>>
	     <COMPARE-WORD-TYPES .BIT <GET-CLASSIFICATION NOUN>>>
	 <FORM COMPARE-WORD-TYPES <FORM WORD-CLASSIFICATION-NUMBER .PTR>
	       .BIT>)
	(T
	 <COND (<COMPARE-WORD-TYPES .BIT <GET-CLASSIFICATION ADJ>>
		<VERSION? (ZIP
			   <FORM COND (<FORM COMPARE-WORD-TYPES .BIT
					     <FORM WORD-CLASSIFICATION-NUMBER
						   .PTR>>
				       <FORM WORD-ADJ-ID .PTR>)>)
			  (T
			   <FORM COMPARE-WORD-TYPES .BIT
				 <FORM WORD-CLASSIFICATION-NUMBER .PTR>>)>)
	       (<COMPARE-WORD-TYPES .BIT <GET-CLASSIFICATION VERB>>
		<FORM COND (<FORM COMPARE-WORD-TYPES .BIT
				  <FORM WORD-CLASSIFICATION-NUMBER .PTR>>
			    <FORM WORD-VERB-STUFF .PTR>)>)
	       (<COMPARE-WORD-TYPES .BIT <GET-CLASSIFICATION DIR>>
		<FORM COND (<FORM COMPARE-WORD-TYPES .BIT
				  <FORM WORD-CLASSIFICATION-NUMBER .PTR>>
			    <FORM WORD-DIR-ID .PTR>)>)
	       (T
		<FORM COMPARE-WORD-TYPES .BIT
		      <FORM WORD-CLASSIFICATION-NUMBER .PTR>>)>)>>

<DEFINE ADJ-USED? (WD1 "OPT" (WD2 <>) (WD3 <>) "AUX" NP)
  <SET NP <GET-NP>>
  <COND (<T? ,NOW-PRSI>
	 <CHECK-ADJ-USED .NP .WD1 .WD2 .WD3>)
	(T
	 <CHECK-ADJ-USED .NP .WD1 .WD2 .WD3>)>>

<DEFINE CHECK-ADJ-USED (PHRASE:<OR FALSE PMEM>
			    WD1 "OPT" (WD2 <>) (WD3 <>)
			    "AUX" NP CT)
  <COND (.PHRASE
	 <COND (<AND <SET NP <NP-ADJ* .PHRASE>>
		     <G? <SET CT <ADJ*-COUNT .NP>> 0>>
		<SET NP <REST-TO-SLOT .NP ADJ*-COUNT 1>>
		<COND (<ZMEMQ .WD1 .NP .CT>
		       .WD1)
		      (<ZMEMQ .WD2 .NP .CT>
		       .WD2)
		      (<ZMEMQ .WD3 .NP .CT>
		       .WD3)>)>)>>

<DEFINE GET-NP ("OPT" (OBJ <>) "AUX" (PRSI? ,NOW-PRSI?))
  <COND (.OBJ
	 <COND (<==? .OBJ ,PRSO> <SET PRSI? <>>)
	       (T <SET PRSI? T>)>)>
  <COND (,OBJ-SWAP
	 <COND (<T? .PRSI?> ,PRSO-NP)
	       (T ,PRSI-NP)>)
	(<T? .PRSI?>
	 ,PRSI-NP)
	(T ,PRSO-NP)>>

<DEFINE NOUN-USED? (WD1 "OPT" (WD2 <>) (WD3 <>) "AUX" NP)
  <SET NP <GET-NP>>
  <COND (<T? ,NOW-PRSI>
	 <CHECK-NOUN-USED .NP .WD1 .WD2 .WD3>)
	(T
	 <CHECK-NOUN-USED .NP .WD1 .WD2 .WD3>)>>

<DEFINE CHECK-NOUN-USED CNP (PHRASE:<OR FALSE PMEM> WD1 "OPT"
			       (WD2 <>) (WD3 <>) "AUX" WD CT)
  <COND (.PHRASE
	 <AND <SET WD <NP-NAME .PHRASE>>
	      <EQUAL? .WD .WD1 .WD2 .WD3>>)>>

<DEFINE END-QUOTE ()
	;<SETG QUOTE-FLAG <>>
	<P-CONT <>>
	<RFATAL>>

<DEFINE NAME? ("OPT" FOO)
	<>>

<DEFINE PRINT-ADJT (TBL:TABLE "AUX" N:FIX)
  <COND (<NOT <0? <SET N <ZGET .TBL 0>>>>
	 <REPEAT ()
	   <PRINTW <ZGET .TBL .N>>
	   <COND (<0? <SET N <- .N 1>>>
		  <RETURN>)>
	   <TELL " ">>)>>

<DEFINE SPECIAL-ADJ-CHECK (ADJ:VWORD OBJ:OBJECT)
 <>>

<DEFINE PRINTW (WRD:VWORD "AUX" TBL)
  <COND (<SET TBL <ZMEMQ .WRD <ZREST ,LONG-WORD-TABLE 2>
			 <ZGET ,LONG-WORD-TABLE 0>>>
	 <TELL <ZGET .TBL 1>>)
	(T
	 <ZPRINTB .WRD>)>>

