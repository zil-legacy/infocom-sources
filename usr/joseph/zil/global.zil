"GLOBALS for MILLIWAYS
Copyright (C) 1988 Infocom, Inc.  All rights reserved."

;"status line stuff"

<CONSTANT S-TEXT 0>
<CONSTANT S-WINDOW 1>

<CONSTANT H-NORMAL 0>
<CONSTANT H-INVERSE 1>
<CONSTANT H-BOLD 2>
<CONSTANT H-ITALIC 4>

<CONSTANT D-SCREEN-ON 1>
<CONSTANT D-SCREEN-OFF -1>
<CONSTANT D-PRINTER-ON 2>
<CONSTANT D-PRINTER-OFF -2>
<CONSTANT D-TABLE-ON 3>
<CONSTANT D-TABLE-OFF -3>
<CONSTANT D-RECORD-ON 4>
<CONSTANT D-RECORD-OFF -4>

<GLOBAL HOST:NUMBER 0> "Host machine."
<GLOBAL WIDTH:NUMBER 0> "Width of screen in chars."
;<GLOBAL MIDSCREEN:NUMBER 0> "Center of screen."
;<GLOBAL CWIDTH:NUMBER 0> "Pixel width of characters."
;<GLOBAL CHEIGHT:NUMBER 0> "Pixel height of characters."

;<REPLACE-DEFINITION INIT-STATUS-LINE>
;<ROUTINE INIT-STATUS-LINE ()
	 <COND (<L? ,WIDTH 38>				
		<TELL "[Screen too narrow.]" CR>
		<QUIT>)>
	 <TELL "[Calling INIT-STATUS-LINE.]" CR>
	 <SETG OHERE <>>
	 <SETG OLD-LEN 0>
	 ;<SETG DO-WINDOW <>>
	 <SPLIT 1>
	 <SCREEN ,S-WINDOW>
	 <HLIGHT ,H-INVERSE>
	 <CURSET 1 1>	 
	 <PRINT-SPACES ,WIDTH>
	 <COND (<G? ,WIDTH 74>       ;"C-128 SCRH is 75 wide"
		<CURSET 1 51>				
		<TELL "Score:">
		<CURSET 1 64>
		<TELL "Moves:">)>
	 <HLIGHT ,H-NORMAL>
	 <SCREEN ,S-TEXT>
	 <RTRUE>>

<CONSTANT SL-TABLE:TABLE <ITABLE NONE 80>>   "status line constructed here"
<GLOBAL OHERE:OBJECT <>>
<GLOBAL OLD-LEN:NUMBER 0>

;<GLOBAL MIDSCREEN:NUMBER 0>

;<REPLACE-DEFINITION UPDATE-STATUS-LINE
;<ROUTINE UPDATE-STATUS-LINE ()
	 <SCREEN ,S-WINDOW>
	 ;<BUFOUT <>>
	 <HLIGHT ,H-NORMAL>
	 <HLIGHT ,H-INVERSE>
	 <COND (<NOT <EQUAL? ,HERE ,OHERE>>
		<SETG OHERE ,HERE>
		;<DIROUT ,D-SCREEN-OFF>	        ; "Screen off."
		<DIROUT ,D-TABLE-ON ,SL-TABLE>  ; "Table on."
		<SAY-HERE>
		<DIROUT ,D-TABLE-OFF> 	        ; "Table off."
	        ;<DIROUT ,D-SCREEN-ON>		; "Screen on."
		<CURSET 1 2>
		<PRINT-SPACES ,OLD-LEN>  ; "Erase old HERE desc"
		<SETG OLD-LEN <GET ,SL-TABLE 0>> ; "Print new HERE desc."
		<CURSET 1 2>
		<SAY-HERE>)>
	 <COND (<G? ,WIDTH 74>      ;"C-128 is 75 wide"
		<CURSET 1 58>
		<TELL N ,SCORE " "> ;"space fixes 110 to 80 score bug"
		<CURSET 1 71>
		<TELL N ,MOVES>)
	       (T
		<DIROUT ,D-TABLE-ON ,SL-TABLE>
		<TELL N ,SCORE "/" N ,MOVES " "> ;"110 to 80 bug"
		<DIROUT ,D-TABLE-OFF>
		<CURSET 1 <- ,WIDTH <+ <GET ,SL-TABLE 0> 1>>>
		<TELL N ,SCORE "/" N ,MOVES " ">)>  ;"110 to 80 bug"
	 ;<HLIGHT ,H-NORMAL>
	 <SCREEN ,S-TEXT>  ;"Back to main screen."
	 <RTRUE>>>

<ROUTINE PRINT-SPACES (CNT)
	 <REPEAT ()
		 <COND (<L? <SET CNT <- .CNT 1>> 0>
			<RETURN>)
		       (T
			<PRINTC 32>)>>>

<ROUTINE SAY-HERE ()   ;PLAYER WAS PROTAGONIST
	 <COND (<ZERO? ,LIT>
		<TELL "Darkness">)
	       (T
		<TELL D ,HERE>
	        <COND (<G? ,WIDTH 74>
		       <COND (<IN? ,PLAYER ,HERE> 
			      <TELL ", in the " D <LOC , PLAYER>>)       
			     (<NOT <IN? ,PLAYER ,HERE>>
		              <TELL ", in the " D <LOC ,PLAYER>>)
			     (,LYING-DOWN
			      <TELL ", lying down">)>)>)>
	 <RTRUE>>

<OBJECT GLOBAL-OBJECTS
	(DESC "GO")
	;(FDESC 0)
	(GENERIC 0)
	(GLOBAL STAIRS)
	(OWNER 0)
	(TEXT 0)
	(THINGS 0)
	(FLAGS	CONTBIT DOORBIT FEMALEBIT
		INVISIBLE LIGHTBIT LOCKEDBIT MUNGBIT
		NARTICLEBIT NDESCBIT ONBIT 
		;PERSONBIT READBIT RMUNGBIT
		SEARCHBIT SECRETBIT SEENBIT SURFACEBIT
		TAKEBIT TOOLBIT TOUCHBIT TRANSBIT TRYTAKEBIT
		VEHBIT VOWELBIT WEAPONBIT WEARBIT WORNBIT OPENABLE)>

<OBJECT LOCAL-GLOBALS
	(LOC GLOBAL-OBJECTS)
	(DESC "LG")
	(SYNONYM L.G)
	(FLAGS NARTICLEBIT)
	(ACTION LOCAL-GLOBALS-F)>

<ROUTINE LOCAL-GLOBALS-F ()
 <COND (<REMOTE-VERB?>
	<RFALSE>)
       (T <NOT-HERE ,LOCAL-GLOBALS>)>>

<OBJECT FRUSTATION
	(LOC GLOBAL-OBJECTS)
	(DESC "puzzle")
	(SYNONYM FRUSTRATION PROBLEM PUZZLE)
	(ACTION FRUSTRATION-F)>

<ROUTINE FRUSTRATION-F ()
	 <COND (<VERB? ENJOY>
		<TELL ,ZEN CR>)>>

<OBJECT STAIRS
	(LOC LOCAL-GLOBALS)
	(DESC "stairs")
	(SYNONYM STAIRS STAIRWAY STAIR)
	;(GENERIC GENERIC-STAIRS)
	(FLAGS SEENBIT)
	;(ACTION UPSTAIRS-DOWNSTAIRS)>

<ROUTINE DO-INSTEAD-OF (OBJ1 OBJ2)
	<COND (<EQUAL? ,PRSI .OBJ2> <PERFORM ,PRSA ,PRSO .OBJ1> <RTRUE>)
	      (<EQUAL? ,PRSO .OBJ2> <PERFORM ,PRSA .OBJ1 ,PRSI> <RTRUE>)
	      ;(T		    <PERFORM ,PRSA ,PRSO ,PRSI> <RTRUE>)
	      (T <V-FOO>)>>

<OBJECT TURN
	(LOC GLOBAL-OBJECTS)
	(ADJECTIVE INT.NUM ;NUMBER FULL)
	(SYNONYM TURN TURNS MINUTE MINUTES)
	(DESC "turn" ;"minute")
	(ACTION TURN-F)>

<ROUTINE TURN-F ()
 <COND (<VERB? USE>
	<PERFORM ,V?WAIT-FOR ,PRSO>
	<RTRUE>)>>

<OBJECT IT
	(LOC GLOBAL-OBJECTS)
	(SYNONYM IT THIS FUCKER SUCKER)
	(DESC "it")
	(FLAGS VOWELBIT NARTICLEBIT)
	(ACTION IT-F)>

<ROUTINE IT-F ()
 <COND (<OR <AND <IOBJ? IT>
		 ;<FSET? ,PRSO ,PERSONBIT>
		 <VERB? ASK-ABOUT ASK-FOR SEARCH-FOR TELL-ABOUT>>
	    <AND <DOBJ? IT>
		 <VERB? ASK-CONTEXT-ABOUT ASK-CONTEXT-FOR FIND ;WHAT>>>
	<TELL "\"I'm not sure what you're talking about.\"" CR>)>>

<OBJECT FLOOR
	(LOC GLOBAL-OBJECTS)
	(DESC "floor")
	;(ADJECTIVE DRAWING ;ROOM GREAT ;HALL)
	(SYNONYM FLOOR ;AREA GROUND CARPET RUG)
	(FLAGS SEENBIT SURFACEBIT OPENBIT)
	(ACTION FLOOR-F)>

<ROUTINE FLOOR-F ("AUX" (OBJ <>) N)
 <COND ;(<REMOTE-VERB?> <RFALSE>)
       (<VERB? CLIMB-ON>
	<ALREADY ,WINNER "on it">)
       (<AND <VERB? PUT THROW-AT>
	     ;<NOT <DOBJ? MOONMIST>>
	     <IOBJ? FLOOR>>
	<MOVE ,PRSO ,HERE>
	<TELL "Okay." CR>
	<RTRUE>)
       (<VERB? EXAMINE LOOK-ON SEARCH SEARCH-FOR>
	<START-SEARCH .OBJ>
	<RTRUE>)>>

<ROUTINE START-SEARCH ("OPTIONAL" (OBJ <>))
	<TELL
"Nothing suspicious meets your eye after a moment's scrutiny. Do you want
to continue?">
	<COND (<NOT <YES?>>
	       <OKAY>
	       <RTRUE>)
	      (T
	       <SETG FOUND-IT .OBJ>
	       <SETG FOUND-LOC ,HERE>
	       <QUEUE I-FOUND-IT <RANDOM 7>>
	       <V-WAIT 8 <> T>
	       <RTRUE>)>>

<GLOBAL FOUND-IT:OBJECT <>>
<GLOBAL FOUND-LOC:OBJECT <>>

<CONSTANT NOTHING-NEW "You don't find anything new there.|">

<ROUTINE I-FOUND-IT ("OPTIONAL" (GARG <>) "AUX" OBJ)
	%<DEBUG-CODE <COND (<OR ,IDEBUG <==? .GARG ,G-DEBUG>>
			    <TELL "[I-FOUND-IT:">
			    <COND (<==? .GARG ,G-DEBUG> <RFALSE>)>)>>
	<COND (<NOT <EQUAL? ,FOUND-LOC ,HERE>>
	       <RFALSE>)
	      (T ;<ZERO? ,FOUND-IT>
	       <TELL ,NOTHING-NEW>
	       <RFATAL>)>>

<OBJECT DANGER
	(LOC GLOBAL-OBJECTS)
	(DESC "danger")
	(SYNONYM DANGER THREAT ATTACK)>

<OBJECT INTNUM
	(LOC GLOBAL-OBJECTS)
	(SYNONYM INT.NUM ;NUMBER)
	(DESC "number")>

<OBJECT YOU
	(LOC GLOBAL-OBJECTS)
	(SYNONYM YOU YOURSELF HIMSELF HERSELF)
	(DESC "self" ;"himself or herself")
	(FLAGS ;NARTICLEBIT)
	(ACTION YOU-F)>

<ROUTINE YOU-F ("AUX" X)
 <COND (<NOT <==? ,WINNER ,PLAYER>>
	<DO-INSTEAD-OF ,WINNER ,YOU>
	<RTRUE>)
       (<AND <VERB? ASK-ABOUT> <IOBJ? YOU>>
	<PERFORM ,V?ASK-ABOUT ,PRSO ,PRSO>
	<RTRUE>)
       (<AND <VERB? THANK>
	     <SET X <QCONTEXT-GOOD?>>>
	<PERFORM ,V?THANK .X>
	<RTRUE>)>>

;<OBJECT HINT
	(DESC "hint")
	(LOC GLOBAL-OBJECTS)
	(SYNONYM HINT HELP)
	(ACTION HINT-F)>

;<ROUTINE HINT-F ()
 <COND (<VERB? FIND>
	<HELP-TEXT>)
       (<VERB? ASK-FOR ASK-CONTEXT-FOR TAKE>
	<MORE-SPECIFIC>)>>

<OBJECT WALL
	(LOC GLOBAL-OBJECTS)
	(DESC "wall")
	(ADJECTIVE BRICK)
	(SYNONYM WALL WALLS BRICK BRICKS)
	(FLAGS SEENBIT SURFACEBIT OPENBIT)
	(ACTION WALL-F)>

<ROUTINE WALL-F ("AUX" OBJ)
 <COND (<AND <VERB? OPEN CLOSE>
	     <T? <SET OBJ <FIND-FLAG-LG ,HERE ,DOORBIT ,SECRETBIT>>>>
	<DO-INSTEAD-OF .OBJ ,WALL>
	<RTRUE>)
       (<VERB? KNOCK>
	<COND (<OR <NOT <FSET? ,HERE ,WEARBIT> ;"WING-ROOMS">
		   <FIND-FLAG-LG ,HERE ,DOORBIT ,SECRETBIT>>
	       <TELL "You hear a hollow sound." CR>)
	      (T <TELL
"Knocking on the walls reveals nothing unusual." CR>)>)>>

;<ROUTINE GENERIC-ROOM (X "OPTIONAL" Y) ,GLOBAL-HERE>

<OBJECT GLOBAL-HERE
	(LOC GLOBAL-OBJECTS)
	(DESC "here")
	(ADJECTIVE THIS)
	(SYNONYM HERE AREA ROOM PLACE)
	;(GENERIC GENERIC-ROOM)
	(FLAGS NARTICLEBIT)
	(ACTION GLOBAL-HERE-F)>

<ROUTINE GLOBAL-HERE-F ("AUX" OBJ (X <>))
 <COND (<VERB? EXAMINE LIE SIT SMELL WALK-TO>
	<DO-INSTEAD-OF ,HERE ,GLOBAL-HERE>
	<RTRUE>)
       (<VERB? PUT PUT-IN ;TIE-TO>
	<MORE-SPECIFIC>)
       (<VERB? SEARCH SEARCH-FOR>
	<COND (<AND <T? ,PRSI>
		    <==? <META-LOC ,PRSI> ,HERE>>
	       <SET X ,PRSI>)
	      (T
	       <SET OBJ <FIRST? ,HERE>>
	       <REPEAT ()
		       <COND (<ZERO? .OBJ>
			      <RETURN>)
			     (<FSET? .OBJ ,SECRETBIT>
			      <SET X .OBJ>
			      <RETURN>)
			     (<AND <NOT <FSET? .OBJ ,PERSONBIT>>
				   <OR <FSET? .OBJ ,CONTBIT>
				       <FSET? .OBJ ,SURFACEBIT>>
				   <OR <SET X <FIND-IN .OBJ ,SECRETBIT>>
				       <SET X <FIND-IN .OBJ ,RMUNGBIT>>>>
			      <FSET .OBJ ,OPENBIT>
			      <RETURN>)
			     (T <SET OBJ <NEXT? .OBJ>>)>>)>
	<START-SEARCH .X>
	<RTRUE>)>>

<OBJECT CHAIR
	(LOC LOCAL-GLOBALS)
	(DESC "chair")
	;(ADJECTIVE WING)
	(SYNONYM CHAIR SEAT CHAIRS BENCH)
	(FLAGS SEENBIT SURFACEBIT ;VEHBIT)
	(ACTION CHAIR-F)>

<ROUTINE CHAIR-F ()
 <COND (<VERB? SIT ;LOOK-UNDER CLIMB-ON ;CLIMB-DOWN BOARD>
	<WONT-HELP>)
       (T <RANDOM-PSEUDO>)>>

<OBJECT TABLE-RANDOM
	(LOC LOCAL-GLOBALS)
	(DESC "table")
	;(ADJECTIVE BILLIARD CARD)
	(SYNONYM TABLE DESK)
	(FLAGS SEENBIT)
	(ACTION RANDOM-PSEUDO)>

<OBJECT SLEEP-GLOBAL
	(LOC GLOBAL-OBJECTS)
	;(ADJECTIVE ;SOME MY)
	(SYNONYM SLEEP)
	(DESC "sleep")
	(FLAGS NARTICLEBIT)
	(ACTION SLEEP-GLOBAL-F)>

<ROUTINE SLEEP-GLOBAL-F ()
 <COND (<VERB? WALK-TO>
	<PERFORM ,V?FAINT>
	<RTRUE>)>>

<ROUTINE ROB (WHAT THIEF "OPTIONAL" (TELL? <>) "AUX" N X (TOLD? <>))
	 <SET X <FIRST? .WHAT>>
	 <REPEAT ()
		 <COND (<NOT .X> <RETURN>)>
		 <SET N <NEXT? .X>>
		 ;<COND (<NOT <FSET? .X ,TAKEBIT>>
			<SET X .N>
			<AGAIN>)>
		 <COND (<AND <NOT .N> .TOLD? .TELL?>
			<TELL " and">)>
		 <SET TOLD? T>
		 <COND (.TELL?
			<TELL the .X>
			<COND (.N <TELL !\,>)
			      (T <TELL ". ">)>)>
		 <MOVE .X .THIEF>
		 ;<FCLEAR .X ,TAKEBIT>
		 <SET X .N>>>

<OBJECT LIGHT-GLOBAL 
	(LOC GLOBAL-OBJECTS)
	(DESC "light")
	(ADJECTIVE FLOOD MOON)
	(SYNONYM LIGHT LIGHTS LAMP MOONLIGHT)
	(FLAGS SEENBIT TRYTAKEBIT)
	(ACTION LIGHT-GLOBAL-F)>

<ROUTINE LIGHT-GLOBAL-F ("AUX" P)
 <COND (<REMOTE-VERB?> <RFALSE>)
       (<VERB? LAMP-ON LAMP-OFF>
	<COND (<AND <OUTSIDE? ,HERE> ;<NOT <EQUAL? ,HERE ,CAR>>>
	       <TELL "You can't reach it from here." CR>)
	      (<VERB? LAMP-ON>
	       <COND (<FSET? ,HERE ,ONBIT>
		      <ALREADY ,LIGHT-GLOBAL "on">)
		     (T
		      <FSET ,HERE ,ONBIT>
		      <OKAY ,LIGHT-GLOBAL "on">)>)
	      (<VERB? LAMP-OFF>
	       <COND (<NOT <FSET? ,HERE ,ONBIT>>
		      <ALREADY ,LIGHT-GLOBAL "off">)
		     (<SET P <FIND-FLAG-HERE-NOT ,PERSONBIT ,MUNGBIT ,PLAYER>>
		      <TELL
D .P " says, \"Please don't leave us in the dark.\"" CR>)
		     (T
		      <FCLEAR ,HERE ,ONBIT>
		      <OKAY ,LIGHT-GLOBAL "off">)>)>)>>
[
<ADJ-SYNONYM MY MINE YOUR>

<OBJECT HANDS
	(LOC GLOBAL-OBJECTS)
	(DESC "your hand")
	(ADJECTIVE BARE MY ;YOUR)
	(SYNONYM HANDS HAND)
	(FLAGS NDESCBIT TOUCHBIT NARTICLEBIT)
	(ACTION HANDS-F)>

<ROUTINE HANDS-F (ACTOR)
	 <COND ;(<VERB? WAVE>
		<PERFORM ,V?WAVE-AT>
		<RTRUE>)
	       (<VERB? SHAKE>
		<COND (<SET ACTOR <FIND-IN ,HERE ,PERSONBIT>>
		       <PERFORM ,V?THANK .ACTOR>
		       <RTRUE>)
		      (T
		       <TELL "Pleased to meet you." CR>)>)>>

;<ROUTINE HANDS-F ("AUX" P A)
 <COND (<NOT <SET P <FIND-BODY ,HANDS>>>
	<RTRUE>)
       (<REMOTE-VERB?>
	<RFALSE>)>
 <COND ;(<EQUAL? .P ,PLAYER>
	<COND (<VERB? BRUSH>
	       <RFALSE>)>)
       (<VERB? KISS>
	<COND (<AND <FSET? .P ,FEMALEBIT>
		    <T? ,GENDER-KNOWN>
		    <NOT <FSET? ,PLAYER ,FEMALEBIT>>>
	       <PERFORM ,V?HELLO .P>)
	      (T
	       <PERFORM ,V?KISS .P>)>
	<RTRUE>)
       (<AND <VERB? SHAKE TAKE> <DOBJ? HANDS>>
	<COND (<T? ,PRSI> ;<ZERO? .P>
	       <SET P ,PRSI>)>
	;<COND (<ZERO? .P>
	       <COND ;(<ADJ-USED? ,W?HER>
		      <SET P <FIND-FLAG-HERE-BOTH ,PERSONBIT,FEMALEBIT,WINNER>>
		      <COND (<ZERO? .P>
			     <TELL "There's no woman here!" CR>
			     <RTRUE>)>)
		     ;(<ADJ-USED? ,W?HIS>
		      <SET P <FIND-FLAG-HERE-NOT ,PERSONBIT ,FEMALEBIT,WINNER>>
		      <COND (<ZERO? .P>
			     <TELL "There's no man here!" CR>
			     <RTRUE>)>)
		     (T
		      <SET P <FIND-FLAG-HERE ,PERSONBIT ,WINNER>>
		      <COND (<ZERO? .P>
			     <TELL "There's no one here!" CR>
			     <RTRUE>)>)>)>
	<PERFORM ,V?HELLO .P>
	<RTRUE>)>>

<OBJECT HEAD
	(LOC GLOBAL-OBJECTS)
	(DESC "your head")
	(ADJECTIVE ;YOUR MY)
	(SYNONYM HEAD FACE)
	(FLAGS NARTICLEBIT)
	(ACTION HEAD-F)>

<ROUTINE HEAD-F ()
 <COND (<VERB? NOD>
	<PERFORM ,V?YES>
	<RTRUE>)
       (<VERB? SHAKE>
	<PERFORM ,V?NO>
	<RTRUE>)>>

<OBJECT EYES
	(LOC GLOBAL-OBJECTS)
	(DESC "your eyes")
	(ADJECTIVE ;YOUR MY)
	(SYNONYM EYE EYES)
	(FLAGS NARTICLEBIT)
	(ACTION EYES-F)>

<ROUTINE EYES-F ()
	 <COND (<VERB? OPEN>
		<TELL "They are." CR>)
	       (<VERB? CLOSE>
		<TELL "That won't help." CR>)>>

<OBJECT TEETH
	(LOC GLOBAL-OBJECTS)
	(DESC "your teeth")
	(ADJECTIVE ;YOUR MY)
	(SYNONYM TEETH)
	(FLAGS NARTICLEBIT)>

<OBJECT EARS
	(LOC GLOBAL-OBJECTS)
	(DESC "your ears")
	(ADJECTIVE ;YOUR MY)
	(SYNONYM EAR EARS)
	(FLAGS NARTICLEBIT)>
]

<OBJECT THIRD-PLANET
	(LOC GLOBAL-OBJECTS)
	(DESC "third planet")
	(ADJECTIVE THIRD BLUE BLUE-GREEN GREEN SMALL)
	(SYNONYM PLANET EARTH)
	(FLAGS NDESCBIT)
	(ACTION THIRD-PLANET-F)>

<ROUTINE THIRD-PLANET-F ()
	 <COND (<VERB? LEAVE DISEMBARK>
		<TELL "You did!" CR>)
	       (<VERB? EXAMINE>
		<TELL
"It is an utterly insignificant little blue-green planet, of the sort
where they probably still wear digital watches." CR>)>>

<OBJECT OBJECT-OF-GAME
	(LOC GLOBAL-OBJECTS)
	(DESC "object of the game")
	(SYNONYM OBJECT GAME GOAL)
	(FLAGS VOWELBIT)
	(ACTION OBJECT-OF-GAME-F)>

<ROUTINE OBJECT-OF-GAME-F ()
	 <COND (<VERB? ASK-ABOUT TALK-ABOUT TELL-ABOUT>
		<TELL "That's for me to know and you to find out." CR>)>>

<OBJECT SKY
	(LOC GLOBAL-OBJECTS)
	(DESC "sky")
	(SYNONYM SKY)
	(ACTION SKY-F)>

<ROUTINE SKY-F ()
	 <COND (<NOT <OUTSIDE? ,HERE>>
		<NOT-HERE ,SKY>)
	       ;(<AND <IN? ,FLEET ,HERE>
		     <VERB? EXAMINE>>
		<TELL
"The sky is filled with the ships of the " D ,FLEET "." CR>)>>
 
<ROUTINE FIND-FLAG-NOT (RM FLAG ;"OPTIONAL" ;(EXCLUDED <>) "AUX" O)
	<SET O <FIRST? .RM>>
	<REPEAT ()
	 <COND (<NOT .O> <RFALSE>)
	       (<AND <NOT <FSET? .O .FLAG>>
		     <NOT <FSET? .O ,INVISIBLE>>
		     ;<NOT <==? .O .EXCLUDED>>>
		<RETURN .O>)
	       (T <SET O <NEXT? .O>>)>>>

<ROUTINE FIND-FLAG-LG (RM FLAG "OPTIONAL" (FLAG2 0) "AUX" TBL O (CNT 0) SIZE)
	 <COND (<SET TBL <GETPT .RM ,P?GLOBAL>>
		<SET SIZE <RMGL-SIZE .TBL>>
		<REPEAT ()
			<SET O <GET/B .TBL .CNT>>
			<COND (<AND <FSET? .O .FLAG>
				    <NOT <FSET? .O ,INVISIBLE>>
				    <OR <0? .FLAG2> <FSET? .O .FLAG2>>>
			       <RETURN .O>)
			      (<IGRTR? CNT .SIZE> <RFALSE>)>>)>>

<ROUTINE FIND-FLAG-HERE (FLAG "OPTIONAL" (NOT1 <>) (NOT2 <>) "AUX" O)
	<SET O <FIRST? ,HERE>>
	<REPEAT ()
	 <COND (<NOT .O> <RFALSE>)
	       (<AND <FSET? .O .FLAG>
		     <NOT <FSET? .O ,INVISIBLE>>
		     <NOT <EQUAL? .O .NOT1 .NOT2>>>
		<RETURN .O>)
	       (T <SET O <NEXT? .O>>)>>>

;<ROUTINE FIND-FLAG-HERE-BOTH (FLAG FLAG2 "OPTIONAL" (NOT2 <>) "AUX" O)
	<SET O <FIRST? ,HERE>>
	<REPEAT ()
	 <COND (<NOT .O> <RFALSE>)
	       (<AND <FSET? .O .FLAG>
		     <FSET? .O .FLAG2>
		     <NOT <FSET? .O ,INVISIBLE>>
		     <NOT <EQUAL? .O .NOT2>>>
		<RETURN .O>)
	       (T <SET O <NEXT? .O>>)>>>

<ROUTINE FIND-FLAG-HERE-NOT (FLAG NFLAG "OPTIONAL" (NOT2 <>) "AUX" O)
	<SET O <FIRST? ,HERE>>
	<REPEAT ()
	 <COND (<NOT .O> <RFALSE>)
	       (<AND <FSET? .O .FLAG>
		     <NOT <FSET? .O .NFLAG>>
		     <NOT <FSET? .O ,INVISIBLE>>
		     <NOT <EQUAL? .O .NOT2>>>
		<RETURN .O>)
	       (T <SET O <NEXT? .O>>)>>> 